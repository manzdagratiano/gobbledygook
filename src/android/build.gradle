import java.lang.Integer
import java.text.SimpleDateFormat
import java.util.Date

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}
apply plugin: 'com.android.application'

/**
  * @brief Method to determine the android:versionCode attribute,
  *        which is an integer.
  *        This just returns the current date in YYYYMMDD format,
  *        which ensures it will be monotonically increasing.
  *        We assume we would not need version granularity within a day.
  */
def computeVersionCode() {
    Date today = new Date()
    SimpleDateFormat fmt = new SimpleDateFormat("yyyyMMdd")
    def versionCode = Integer.parseInt(fmt.format(today))
    return versionCode
}

/**
  * @brief Method to compute the android:versionName attribute,
  *        which is a string.
  *        The versionName is computed as 2.YYYYMMDD.gitCommitSHA
  *        in order to uniquely identify the version.
  *        Major 2 to maintain compatibility with the browser version.
  */
def computeVersionName() {
    def gitCommitCommand = "git rev-parse --short HEAD"
    def gitBranchCommand = "git rev-parse --abbrev-ref HEAD"
    def gitCommit = gitCommitCommand.execute()
    def gitBranch = gitBranchCommand.execute()
    def versionName = "2." + \
                      Integer.toString(computeVersionCode()) + ".0." + \
                      gitBranch.text.trim() + "@" + gitCommit.text.trim()
    return versionName
}

repositories {
    mavenCentral()
}

dependencies {
    /**
      * Spongycastle
      */
    compile "com.madgag.spongycastle:core:1.54.0.0"
    compile "com.madgag.spongycastle:prov:1.54.0.0"

    /**
      * Unit-Testing Infrastructure  
      */
    // JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // The Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'
}

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 23
        versionCode computeVersionCode()
        versionName computeVersionName()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }

    productFlavors {
        gobbledygook {
            applicationId 'io.tengentoppa.gobbledygook'
            versionCode computeVersionCode()
            versionName computeVersionName()
        }
        krunch {
            applicationId 'io.tengentoppa.krunch'
            versionCode computeVersionCode()
            versionName computeVersionName()
        }
    }

    lintOptions {
        abortOnError false
    }

}

ext {
    supportLibVersion = '24.2.1'
}

dependencies {
    /** The following android libraries are NOT in mavenCentral();
      * they require the android-support-repository package to be installed;
      * the path to the repository is automatically determined using
      * the "sdk.dir" property in the "local.properties" file
      */
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:preference-v7:${supportLibVersion}"
    compile "com.android.support:preference-v14:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    /**
      * At this time, there are no jar files that need to be compiled
      * (now that spongycastle can be fetched from maven central).
      * compile fileTree(dir : 'libs', include : ['*.jar'])
      */
}
